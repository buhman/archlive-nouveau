# Maintainer: Tim Jester-Pfadt <t.jp(at)gmx.de>

pkgname=valgrind-mmt-git
pkgver=12221.10cfc45b2
pkgrel=2
pkgdesc="Valgrind with mmt tool to trace application accesses to mmaped memory"
arch=('i686' 'x86_64')
url="https://github.com/envytools/valgrind.git"
license=('GPL')
conflicts=(valgrind)
replaces=(valgrind)
depends=('glibc=2.26' 'perl')
options=('!emptydirs')
source=('git+https://github.com/envytools/valgrind.git#branch=mmt-3.11')
sha256sums=('SKIP')

prepare() {
  if [ -d "$srcdir"/valgrind/VEX ]; then
    rm -rf "$srcdir"/valgrind/VEX
  fi
  git clone -b VEX_3_11_BRANCH https://github.com/envytools/VEX.git "$srcdir"/valgrind/VEX
}

build() {
  # valgrind does not like stack protector flags
  CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
  CFLAGS=${CFLAGS/-fstack-protector-strong/}
  CXXFLAGS=${CXXFLAGS/-fstack-protector-strong/}

  cd "$srcdir"/valgrind
  autoreconf -fi
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir"/valgrind
  make DESTDIR="$pkgdir" install
}

pkgver() {
  cd "$srcdir"/valgrind
  echo $(git rev-list --count mmt-3.11).$(git rev-parse --short mmt-3.11)
}
